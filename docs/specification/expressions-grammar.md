# Грамматика выражений языка StarLight

## Синтакис выражений

Выражения в StarLight могут содержать :
- Числовые литералы (целые и с плавающей точкой)
- Строковые литералы
- Логические литералы (ИСТИНА, ЛОЖЬ)
- Унарные и бинарные опреторы
- Вызов встроенных функций
- Идентификаторы переменных
- Скобки для группировки выражений
- Константы
  

## Операторы

**Арифметические операторы:**

| Символы | Операция | Ассоциативность |
|---------|----------|-----------------|
| `+` | Сложение или унарный `+` | Левая |
| `-` | Вычитание или унарный `-` | Левая |
| `*` | Умножение | Левая |
| `/` | Деление | Левая |
| `%` | Остаток от деления | Левая |
| `**` | Возведение в степень | Правая |

**Операторы сравнения:**

| Символы| Операция |
|--------|----------|
| `==` | Равно |
| `!=` | Не равно |
| `>` | Больше |
| `>=` | Больше или равно |
| `<` | Меньше |
| `<=` | Меньше или равно |

**Логические операторы:**

| Символы | Операция | Ассоциативность |
|---------|----------|-----------------|
| `!` |Логическое НЕ | Правая |
| `&&` | Логическое И | Левая |
| `\|\|` | Логическое ИЛИ | Левая |

## Приоритеты операторов

|Приоритет (по убыванию)| Операторы | Описание |
|-----------------------|-----------|----------|
|1| `**` | Возведение в степень |
|2| `!`, `+`, `-` (унарные) | Унарные операторы|
|3|`*`, `/`, `%` | Мультипликативные |
|4|`+`, `-` (бинарные)| Аддитивные |
|5|`>`, `>=`, `<`, `<=`| Сравнения|
|6| `==`, `!=` | Равенство |
|7|`&&` | Логическое И|
|8|`\|\|`| Логическое ИЛИ|

## Встроенные функции и константы

**Встроенные функции для числа**
- `abs(x)` - модуль
  
- `min(x, y, ...)` - минимальное значение
  
- `max(x, y, ...)` - максимальное значение
  
**Встроенные тригонометрические функции**

- sin(x) - синус угла

- cos(x) - косинус угла

- tan(x) - тангенс угла

**Встроенные функции округления**
  
- `round(x)` - округление до ближайшего целого 
  
- `ceil(x)` - возвращает ближайшее целое, большее или равное переданному
  
- `floor(x)` - возвращает ближайшее целое, меньшее или равное переданному

## Грамматика в нотации EBNF

**Базовые символы**

```
digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;

letter = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j"
       | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t"
       | "u" | "v" | "w" | "x" | "y" | "z"
       | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J"
       | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T"
       | "U" | "V" | "W" | "X" | "Y" | "Z"
       | "а" | "б" | "в" | "г" | "д" | "е" | "ё" | "ж" | "з" | "и"
       | "й" | "к" | "л" | "м" | "н" | "о" | "п" | "р" | "с" | "т"
       | "у" | "ф" | "х" | "ц" | "ч" | "ш" | "щ" | "ъ" | "ы" | "ь"
       | "э" | "ю" | "я"
       | "А" | "Б" | "В" | "Г" | "Д" | "Е" | "Ё" | "Ж" | "З" | "И"
       | "Й" | "К" | "Л" | "М" | "Н" | "О" | "П" | "Р" | "С" | "Т"
       | "У" | "Ф" | "Х" | "Ц" | "Ч" | "Ш" | "Щ" | "Ъ" | "Ы" | "Ь"
       | "Э" | "Ю" | "Я" ;
```

**Литералы и базовые эелементы**
```
literal = number | string | boolean;

number = integer | real;

integer = digit, { digit };

real = digit, { digit }, ".", digit, { digit }, [ ("е" | "E"), ["+" | "-"], digit, { digit } ];

string = "'", { character - "'" | escape_sequence }, "'" 
       | "\"\"\"", { any_character - "\"\"\"" | escape_sequence }, "\"\"\"" ;

boolean = "ИСТИНА" | "ЛОЖЬ" ;

escape_sequence = 
  "\\", ( "'" | "\"" | "\\" | "n" | "t" | "u", "{", hex_digit, hex_digit, hex_digit, hex_digit, "}" ) ;

hex_digit = 
  digit | "A" | "B" | "C" | "D" | "E" | "F" | "a" | "b" | "c" | "d" | "e" | "f" 

any_character = ? любой символ Unicode ? ;

```

**Типы данных**
```
type = "квазар" | "нова" | "луч" | "вакуум" | "галактика" ;
```

**Идентификаторы**
```
identifier = (letter | "_"), { letter | digit | "_" };
```

**Основная конструкция программы**
```
program = "ЗВЕЗДА", { statement }, "ЗАКРЫТАЯ ЗВЕЗДА" ;

statement = variable_declaration | assignment | function_call | if_statement | while_statement | for_statement | return_statement ;
```

**Объявления**
```
variable_declaration = "СВЕТ", identifier, ":", type, [ "=", expression ], ";" ;

constant_declaration = "КОНСТЕЛЛАЦИЯ", identifier, ":", type, "=", expression, ";" ;

function_declaration = "ФОТОН", identifier, "(", [ parameter_list ], ")", [ ":", type ], "{", { statement }, "}" ;

parameter_list = parameter, { ",", parameter } ;

parameter = identifier, ":", type ;
```

**Инструкции**
```
assignment = identifier, ( "=" | "+=" | "-=" | "*=" | "/=" | "**=" ), expression, ";" ;

function_call = ( "ИЗЛУЧАТЬ" | "ПРИЕМ_СИГНАЛА" | identifier ), "(", [ argument_list ], ")", ";" ;

argument_list = expression, { ",", expression } ;

if_statement = "ЕСЛИ", "(", expression, ")", "{", { statement }, "}", [ "ИЛИ_НЕТ", "{", { statement }, "}" ] ;

while_statement = "ОРБИТА", "(", expression, ")", "{", { statement }, "}" ;

for_statement = "СПЕКТР", "(", [ for_init ], ";", [ expression ], ";", [ for_update ], ")", "{", { statement }, "}" ;

for_init = variable_declaration | assignment | expression ;

for_update = assignment | function_call | expression ;

return_statement = "ВЕРНУТЬ", [ expression ], ";" ;
```

**Выражения**
```
expression = logical_or ;

logical_or = logical_and, { "||", logical_and } ;

logical_and = equality, { "&&", equality } ;

equality = comparison, [ ( "==" | "!=" ), comparison ] ;

comparison = additive, [ ( ">" | ">=" | "<" | "<=" ), additive ] ;

additive = multiplicative, { ( "+" | "-" ), multiplicative } ;

multiplicative = power, { ( "*" | "/" | "%" ), power } ;

power = unary, [ "**", power ] ;

unary = [ "+" | "-" | "!" ], primary ;

primary = literal | identifier | function_call | "(", expression, ")" | array_literal ;

array_literal = "[", [ expression, { ",", expression } ], "]" ;
```